WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

ident = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }
typ = { "unit" | "bool" | "int" | "float" | "string" }
arg_list = { (ident ~ ("," ~ ident)* ~ ":" ~ typ)* }
call = { ident ~ "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }
bool = { "true" | "false" }
int = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
	!("\"" | "\\") ~ ANY
	| "\\" ~ ("\"" | "\\" | "n" | "r" | "t")
}
constant = { bool | int | string }
expr = { call | constant }
eq = { ident ~ "=" ~ expr ~ ";" }
node = {
	"node" ~ ident ~ "(" ~ arg_list ~ ")" ~
	"returns" ~ "(" ~ arg_list ~ ")" ~ ";" ~
	"let" ~ eq* ~ "tel"
}
file = { SOI ~ node* ~ EOI }
