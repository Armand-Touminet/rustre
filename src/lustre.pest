WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

ident = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }
arg = { ident ~ ":" ~ ident }
arg_list = { (arg ~ ("," ~ arg)*)? }
call = { ident ~ "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }
bool = { "true" | "false" }
int = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
	!("\"" | "\\") ~ ANY
	| "\\" ~ ("\"" | "\\" | "n" | "r" | "t")
}
constant = { bool | int | string }
expr = { call | constant }
eq = { ident ~ "=" ~ expr ~ ";" }
eq_list = { eq* }
node = {
	"node" ~ ident ~ "(" ~ arg_list ~ ")" ~
	"returns" ~ "(" ~ arg_list ~ ")" ~ ";" ~
	"let" ~ eq_list ~ "tel"
}
node_list = { node* }
file = _{ SOI ~ node_list ~ EOI }
